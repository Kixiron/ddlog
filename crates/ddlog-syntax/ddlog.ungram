Root = Item*

Item =
    FunctionDef
    | StructDef
    | EnumDef
    | ConstDef
    | UseDef
    | ImplBlock
    | TypeAlias
    // TODO: Relations & Clauses

FunctionDef = Attribute* Modifier* keyword:'fn' name:FunctionName Generics args:FunctionArgs ret:FunctionReturn? body:Block
FunctionName = 'ident'
FunctionArgs = '(' args:FunctionArg* ')'
FunctionArg = Attribute* binding:Pattern ':' ty:Type ','*
FunctionReturn = '->' return_ty:Type

StructDef = Attribute* Modifier* keyword:'struct' name:StructName fields:StructFields
StructName = 'ident'
StructFields = BracketedStructFields | TupleStructFields
BracketedStructFields = '{' fields:BracketedStructField* '}'
BracketedStructField = name:StructFieldName ':' ty:Type ','*
StructFieldName = 'ident'
TupleStructFields = '(' TupleStructField* ')'
TupleStructField = ty:Type ','*

EnumDef = Attribute* Modifier* keyword:'enum' name:EnumName variants:EnumVariants
EnumName = 'ident'
EnumVariants = '{' variants:EnumVariant* '}'
EnumVariant = EnumVariantName EnumVariantBody
EnumVariantBody = VariantTuple | VariantStruct
EnumVariantName = 'ident'
VariantTuple = '(' VariantTupleElem* ')'
VariantTupleElem = ty:Type ','*
VariantStruct = '{' fields:VariantStructField* '}'
VariantStructField = name:VariantStructFieldName ':' ty:Type ','*
VariantStructFieldName = 'ident'

ConstDef = Attribute* Modifier* keyword:'const' name:ConstName '=' ConstValue ';'
// TODO: Could be a pattern
ConstName = 'ident'
ConstValue = value:Expr

UseDef = Attribute* Modifier* keyword:'use' UseTree ';'
UseTree = Path UseBranchOrAlias?
UseBranchOrAlias = UseBranch | UseAlias
UseAlias = 'as' UseAliasName
UseAliasName = 'ident'
UseBranch = '{' (UseTree ','*)* '}'

// TODO: `impl Trait for Type { ... }`
ImplBlock = Attribute* Modifier* keyword:'impl' target:Type contents:ImplBlockContents
ImplBlockContents = '{' Item* '}'

TypeAlias =  Attribute* Modifier* keyword:'type' alias:Type '=' original:Type ';'*

Attribute = '#[' AttrPair* ']'
AttrPair = name:AttrName ('=' Expr ','*)?
AttrName = 'ident'

Modifier = 'pub' // 'input' | 'output'

Pattern = VarRef | TuplePattern | StructPattern
TuplePattern = '(' elements:TuplePatternElem* ')'
TuplePatternElem = Pattern ','*
StructPattern = '{' fields:StructPatternField* '}'
StructPatternField = name:StructPatternFieldName (':' alias:Pattern)? ','*
StructPatternFieldName = 'ident'

Type = GenericType | TupleType | FunctionType
Path = '::'? head:PathSegment tail:PathTail*
PathTail = '::' tail:PathSegment
PathSegment = 'ident'

GenericType = Path Generics?
Generics = '<' generics:GenericArg* '>'
GenericArg = ty:Type ','*

TupleType = '(' elements:TupleTypeElem* ')'
TupleTypeElem = ty:Type ','*

FunctionType = 'fn' args:FunctionTypeArgs ret:FunctionReturnType?
FunctionTypeArgs = '(' args:FunctionTypeArg* ')'
FunctionTypeArg = ty:Type ','*
FunctionReturnType = '->' ty:Type

Block = '{' statements:Stmt* '}'
Stmt = ExprStmt | VarDecl

ExprStmt = Expr ';'*
VarDecl = 'let' binding:Pattern (':' ty:Type)? '=' value:Expr ';'*

Expr =
    Literal
    | VarRef
    | QualifiedRef
    | Assign
    | ParenExpr
    | BinExpr
    | IfExpr
    | RetExpr
    | BreakExpr
    | ContinueExpr
    | UnaryExpr
    | Block
    | WhileExpr
    | ForExpr
    | LoopExpr
    | MatchExpr
    | ClosureExpr
    | FieldAccess
    | ArrayAccess
    | FunctionCall
    | StructInitExpr
    | TupleInitExpr
    | ArrayInitExpr
    | RangeExpr
    // TODO: `if let` and `while let` expressions

VarRef = 'ident'
QualifiedRef = Path

WhileExpr = 'while' cond:Expr Block
ForExpr = 'for' binding:Pattern 'in' iter:Expr Block
LoopExpr = 'loop' Block

IfExpr = IfBlock* ElseBlock?
IfBlock = leading_else:'else'? 'if' cond:Expr Block
ElseBlock = 'else' Block

MatchExpr = 'match' scrutinee:Expr '{' MatchArm* '}'
MatchArm = binding:Pattern '=>' body:Expr ','* 

Assign = binding:Pattern AssignOp value:Expr
AssignOp =
    '='
    | '+='
    | '-='
    | '/='
    | '*='
    | '%='
    | '&='
    | '|='
    | '^='
    | '<<='
    | '>>='

ParenExpr = '(' inner:Expr ')'

ClosureExpr = start_pipe:'|' args:ClosureArg* end_pipe:'|' body:Expr
ClosureArg = binding:Pattern (':' ty:Type)? ','*

FieldAccess = Expr '.' FieldAccessor
FieldAccessor = FieldAccessorName | Number
FieldAccessorName = 'ident'
ArrayAccess = Expr '[' index:Expr ']'

FunctionCall = func:Expr '(' args:FunctionCallArg* ')'
// Future Possibility: Named arguments
FunctionCallArg = arg:Expr ','*

StructInitExpr = ty:Path '{' fields:StructInitField* '}'
StructInitField = field:'ident' (':' value:Expr)? ','*

TupleInitExpr = '(' elems:TupleExprElem* ')'
TupleExprElem = Expr ','*

ArrayInitExpr = '[' ArrayExprElem ']'
ArrayExprElem = Expr ','*

RangeExpr = start:Expr? RangeOp end:Expr?
RangeOp = '..' | '..='

// TODO: Floats
Literal = Bool | Number | String
Bool = 'true' | 'false'
Number = 'number_literal'
String = 'string_literal'

RetExpr = 'return' expr:Expr
BreakExpr = 'break' expr:Expr
ContinueExpr = 'continue'

UnaryExpr = op:UnaryOp expr:Expr
UnaryOp = '!' | '-'

BinExpr = lhs:Expr op:BinOp rhs:Expr
BinOp =
    '+'
    | '-'
    | '*'
    | '/' 
    | '%'
    | '|'
    | '^'
    | '&'
    | '<<'
    | '>>'
    | 'and'
    | 'or'
    | '=='
    | '!='
    | '>'
    | '>='
    | '<'
    | '<='

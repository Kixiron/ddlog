Root = Item

Item = FuncDef | RelationDef

RelationDef = RelMods 'relation' RelName RelCols
RelMods = ('input' | 'output')?
RelName = 'ident'
RelCols = '(' (RelCol (',' FuncArg)* ','?)? ')'
RelCol = Pat ':' Type

FuncDef = FuncMods 'function' name:FuncName args:FuncArgs (':' Type)? Block
FuncMods = 'extern'
FuncName = 'ident'
FuncArgs = '(' (FuncArg (',' FuncArg)* ','?)? ')'
FuncArg = Pat ':' Type

Type = 'ident'

Block = '{' (Stmt ';'*)* '}'
Stmt =
    Expr ';'
    | VarDecl
    | IfStmt

VarDecl = 'var' Pat '=' Expr ';'

IfStmt = IfBlock ('else' IfBlock)* ElseBlock
IfBlock = 'if' Expr Block
ElseBlock = ('else' Block)?

Pat = 'ident' | TuplePat
TuplePat = '(' Pat (',' Pat)* ','? ')'

Expr =
    Literal
    | VarRef
    | Assign
    | ParenExpr
    | BinExpr
    | IfStmt
    | RetExpr
    | UnaryExpr
    | Block

VarRef = 'ident'

Assign = Pat '=' Expr

ParenExpr = '(' Expr ')'

// TODO: Floats
Literal = Bool | 'number' | 'string'
Bool = 'true' | 'false'

RetExpr = 'return' Expr

UnaryExpr = UnaryOp Expr
UnaryOp = '!' | '-'

BinExpr = Expr BinOp Expr
BinOp =
    '+'
    | '-'
    | '*'
    | '/'
    | '%'
    | '=>'
    | '|'
    | '^'
    | '&'
    | '<<'
    | '>>'
    | 'and'
    | 'or'
    | '=='
    | '!='
    | '>'
    | '>='
    | '<'
    | '<='
